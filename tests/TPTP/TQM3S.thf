include('SUMOonly.thf').

%(believes Bill (attribute Earth Flat))

thf(s__Bill_tp,type,(s__Bill : $i)).
thf(s__Joe_tp,type,(s__Joe : $i)).
thf(s__Flat_tp,type,(s__Flat : $i)).  % in MILO
thf(s__Circumnavigation_tp,type,(s__Circumnavigation : $i)).
thf(s__PlanetEarth_tp,type,(s__PlanetEarth : $i)).  % in Geography.kif

thf(kb1,axiom,(
  (![W:$w, W2:$w] : ((s__accreln @ s__believes @ s__Bill @ W @ W2) =>
    ( (s__attribute @ s__PlanetEarth @ s__Flat @ W2)))))).

% believes has fewer successor worlds than knows
% a quantifier over fewer things is easier, therefore
% the implication is "backwards" from what one would
% expect

thf(kb3,axiom,(
  (![W:$w, W2:$w, H:$i]: ((s__accreln @ s__believes @ H @ W @ W2) =>
    (s__accreln @ s__knows @ H @ W @ W2))))).

% "Is the earth flat." should be no, satisfiable
% query: (attribute Earth Flat) - Satisfiable, correct

%tff(conj,conjecture,( ![W:$w]: (s__attribute(s__PlanetEarth,s__Flat,W)))).

% "Does bill know that the earth is flat?"
% query: (knows Bill (attribute Earth Flat)) -  Satisfiable, correct (he doesn't know that)

% thf(conj,conjecture,( ![W:$w, W2:$w]: ((s__accreln @ s__knows @ bill @ W @ W2) => ((s__attribute @ s__PlanetEarth @ s__Flat @ W2))))).

% If you try to circumnavigate a flat earth you will fall off the edge and die.
% Ignore the temporal aspect for now.
% (=>
%  (and
%    (instance ?C Circumnavigation)
%    (located ?C ?E)
%    (attribute ?E Flat)
%    (agent ?C ?A))
%  (attribute ?A Dead)))

thf(kb4,axiom,
  (![C:$i, E:$i, A:$i, W:$w]:
    (((s__instance @ C @ s__Circumnavigation @ W) &
      (s__located @ C @ E @ W) &
      (s__attribute @ E @ s__Flat @ W) &
      (s__agent @ C @ A @ W)) =>
    (s__attribute @ A @ s__Dead @ W)))).

% We want to say if (X => Y) and A believes X then A believes Y - requires true HOL

thf(kb5,axiom,
  (![W:$w,W2:$w,W3:$w,X:$o,Y:$o,A:$i]:(
    (((X => Y) &
     ((s__accreln @ s__believes @ A @ W @ W2) => X)) =>
     ((s__accreln @ s__believes @ A @ W2 @ W3) => Y))))).

thf(kb6,axiom,
  (![W:$w, W2:$w] : (
    (?[C:$i, E:$i] : (
      (s__accreln @ s__believes @ s__Bill @ W @ W2) =>
      ((s__instance @ C @ s__Circumnavigation @ W) &
       (s__located @ C @ E @ W2) &
       (s__attribute @ E @ s__Flat @ W2) &
       (s__agent @ C @ s__Joe @ W2))))))).

%thf(conj,conjecture,(
%  ![W:$w, W2:$w]:
%    ((s__accreln @ s__believes @ s__Bill @ W @ W2) =>
%     ((s__attribute @ s__Joe @ s__Dead @ W2))))).

thf(conj,conjecture,( ?[P:$i] : (
  ![W:$w, W2:$w]:
    ((s__accreln @ s__believes @ s__Bill @ W @ W2) =>
     ((s__attribute @ P @ s__Dead @ W2)))))).

%thf(conj,conjecture,( ! [V__W1:$w,V__W2:$w] : (((s__accreln @ s__believes__m @ s__Bill @ V__W1 @ V__W2) => (s__attribute @ s__Joe @ s__Dead @ V__W2)) ) )).

