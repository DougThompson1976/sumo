% from QMLTP problems http://www.iltp.de/qmltp/problems.html
% APM001+1.p converted to SUMO

thf(worlds_tp,type,(w : $tType)).
thf(modals_tp,type,(m : $tType)).

thf(attribute_tp,type,(attribute : ($i > $i > w > $o))).
thf(destination_tp,type,(destination : ($i > $i > w > $o))).
thf(instance_tp,type,(instance : ($i > $i > $o))).
thf(monetaryValue_tp,type,(monetaryValue : ($i > $i > w > $o))).
thf(measureFn_tp,type,(measureFn : ($i > $i > $i))). % change $real to $i since THF doesn't support it
thf(accreln_tp,type,(accreln : (m > $i > w > w > $o))).

thf(firstClass_tp,type,(firstClass : $i)).
thf(secondClass_tp,type,(secondClass : $i)).
thf(necessity_tp,type,(necessity : $i)).
thf(flying_tp,type,(flying : $i)).
thf(usDollar_tp,type,(usDollar : $i)).
thf(paris_tp,type,(paris : $i)).
thf(myFlight_tp,type,(myFlight : $i)).
thf(seventy_tp,type,(seventy : $i)). % have to use symbols since numbers aren't supported
thf(ninetyFive_tp,type,(ninetyFive : $i)). % have to use symbols since numbers aren't supported

thf(modalAttribute_tp,type,(modalAttribute : m)).

thf(axiom_t_modalAttribute,axiom,(! [W:w] : ((accreln @ modalAttribute @ necessity @ W @ W)))).

%qmf(law1, axiom,
%(#box : ((dest(paris) & class(first))  => price(ninetyfive)))).

% (modalAttribute
%  (=> 
%    (and
%      (instance ?F Flying)
%      (attribute ?F FirstClass)
%      (destination ?F Paris))
%    (monetaryValue ?F (MeasureFn 95 USDollar)))
%    Necessity)
    
thf(kb1,axiom,
  (![W:w, W2:w] : 
    ((accreln @ modalAttribute @ necessity @ W @ W2) =>
    (![F:$i]: (
      ((instance @ F @ flying) & (attribute @ F @ firstClass @ W2) & (destination @ F @ paris @ W2)) =>
      (monetaryValue @ F @ (measureFn @ ninetyFive @ usDollar) @ W2)))))).
      
% qmf(law2, axiom,
%(#box : ((dest(paris) & class(second)) => price(seventy)))).

thf(kb2,axiom,
  (![W:w, W2:w] : 
    ((accreln @ modalAttribute @ necessity @ W @ W2) =>
    (![F:$i]: (
      ((instance @ F @ flying) & 
       (attribute @ F @ secondClass @ W2) & 
       (destination @ F @ paris @ W2)) =>
      (monetaryValue @ F @ (measureFn @ seventy @ usDollar) @ W2)))))).
      
%qmf(law3, axiom,
%(#box : (~ (class(first) & class(second))))).

thf(kb3,axiom,
  ((instance @ myFlight @ flying))).
  
thf(kb4,axiom,
  (![W:w, W2:w] : 
    ((accreln @ modalAttribute @ necessity @ W @ W2) =>
    (~ ((attribute @ myFlight @ firstClass @ W2) & (attribute @ myFlight @ secondClass @ W2)))))).
        
%qmf(law4, axiom,
%(#box : (~ (price(ninetyfive) & price(seventy))))).

thf(kb5,axiom,
  (![W:w, W2:w] : 
    ((accreln @ modalAttribute @ necessity @ W @ W2) =>
    (~ ((monetaryValue @ myFlight @ (measureFn @ ninetyFive @ usDollar) @ W2) & 
        (monetaryValue @ myFlight @ (measureFn @ seventy @ usDollar) @ W2)))))).

%qmf(belief1, axiom,
%(#box : (dest(paris)))). 

thf(kb6,axiom,
  (![W:w, W2:w] : 
    ((accreln @ modalAttribute @ necessity @ W @ W2) =>
    (destination @ myFlight @ paris @ W2)))).
        
%qmf(belief2, axiom,
%(#box : (class(second)))).

thf(kb7,axiom,
  (![W:w, W2:w] : 
    ((accreln @ modalAttribute @ necessity @ W @ W2) =>
    (attribute @ myFlight @ secondClass @ W2)))).
    
%qmf(con, conjecture,
%(#box : (price(seventy)))).
% below working - Refutation

%thf(conj,conjecture,
%  (![W:w, W2:w] : 
%    (accreln(modalAttribute,necessity,W,W2) =>
%    (monetaryValue @ myFlight @ (measureFn @ 70.0 @ usDollar) @ W2)))).
   
% below works but doesn't return the binding of 70.0 since the 
% existential isn't in the outermost scope

%thf(conj,conjecture,
%  (![W:w, W2:w] : (? [N:$real] : (
%    ((accreln @ modalAttribute @ necessity @ W @ W2) =>
%    (monetaryValue @ myFlight @ (measureFn @ N @ usDollar) @ W2)))))).
       
% works with binding since existential is in outermost scope       
thf(conj,conjecture,
  (? [N:$i] : (![W:w, W2:w] : ( 
    ((accreln @ modalAttribute @ necessity @ W @ W2) =>
    (monetaryValue @ myFlight @ (measureFn @ N @ usDollar) @ W2)))))).
        
