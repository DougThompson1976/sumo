% from QMLTP problems http://www.iltp.de/qmltp/problems.html
% APM001+1.p converted to SUMO

tff(worlds_tp,type,(w : $tType)).
tff(modals_tp,type,(m : $tType)).

tff(attribute_tp,type,(attribute : ($i * $i * w) > $o)).
tff(destination_tp,type,(destination : ($i * $i * w) > $o)).
tff(instance_tp,type,(instance : ($i * $i) > $o)).
tff(monetaryValue_tp,type,(monetaryValue : ($i * $i * w) > $o)).
tff(measureFn_tp,type,(measureFn : ($real * $i) > $i)).
tff(accreln_tp,type,(accreln : (m * $i * w * w) > $o)).

tff(firstClass_tp,type,(firstClass : $i)).
tff(secondClass_tp,type,(secondClass : $i)).
tff(necessity_tp,type,(necessity : $i)).
tff(flying_tp,type,(flying : $i)).
tff(usDollar_tp,type,(usDollar : $i)).
tff(paris_tp,type,(paris : $i)).
tff(myFlight_tp,type,(myFlight : $i)).

tff(modalAttribute_tp,type,(modalAttribute : m)).

tff(axiom_t_modalAttribute,axiom,(! [W:w] : (accreln(modalAttribute,necessity,W,W)))).

%qmf(law1, axiom,
%(#box : ((dest(paris) & class(first))  => price(ninetyfive)))).

% (modalAttribute
%  (=> 
%    (and
%      (instance ?F Flying)
%      (attribute ?F FirstClass))
%    (monetaryValue ?F (MeasureFn 95 USDollar)))
%    Necessity)
    
tff(kb1,axiom,
  (![W:w, W2:w] : 
    (accreln(modalAttribute,necessity,W,W2) =>
    (![F:$i]: (
      (instance(F,flying) & attribute(F,firstClass,W2) & destination(F,paris,W2)) =>
      monetaryValue(F,measureFn(95.0,usDollar),W2)))))).
      
% qmf(law2, axiom,
%(#box : ((dest(paris) & class(second)) => price(seventy)))).

tff(kb1,axiom,
  (![W:w, W2:w] : 
    (accreln(modalAttribute,necessity,W,W2) =>
    (![F:$i]: (
      (instance(F,flying) & attribute(F,secondClass,W2) & destination(F,paris,W2)) =>
      monetaryValue(F,measureFn(70.0,usDollar),W2)))))).
      
%qmf(law3, axiom,
%(#box : (~ (class(first) & class(second))))).

tff(kb0,axiom,
  (instance(myFlight,flying))).
  
tff(kb1,axiom,
  (![W:w, W2:w] : 
    (accreln(modalAttribute,necessity,W,W2) =>
    (~ (attribute(myFlight,firstClass,W2) & attribute(myFlight,secondClass,W2)))))).
        
%qmf(law4, axiom,
%(#box : (~ (price(ninetyfive) & price(seventy))))).

tff(kb2,axiom,
  (![W:w, W2:w] : 
    (accreln(modalAttribute,necessity,W,W2) =>
    (~ (monetaryValue(myFlight,measureFn(95.0,usDollar),W2) & 
        monetaryValue(myFlight,measureFn(70.0,usDollar),W2)))))).

%qmf(belief1, axiom,
%(#box : (dest(paris)))). 

tff(kb3,axiom,
  (![W:w, W2:w] : 
    (accreln(modalAttribute,necessity,W,W2) =>
    destination(myFlight,paris,W2)))).
        
%qmf(belief2, axiom,
%(#box : (class(second)))).

tff(kb4,axiom,
  (![W:w, W2:w] : 
    (accreln(modalAttribute,necessity,W,W2) =>
    attribute(myFlight,secondClass,W2)))).
    
%qmf(con, conjecture,
%(#box : (price(seventy)))).
% below working - Refutation

%tff(conj,conjecture,
%  (![W:w, W2:w] : 
%    (accreln(modalAttribute,necessity,W,W2) =>
%    monetaryValue(myFlight,measureFn(70.0,usDollar),W2)))).
   
% below works but doesn't return the binding of 70.0 since the 
% existential isn't in the outermost scope

%tff(conj,conjecture,
%  (![W:w, W2:w] : (? [N:$real] : (
%    (accreln(modalAttribute,necessity,W,W2) =>
%    monetaryValue(myFlight,measureFn(N,usDollar),W2)))))).
       
% works with binding since existential is in outermost scope       
tff(conj,conjecture,
  (? [N:$real] : (![W:w, W2:w] : ( 
    (accreln(modalAttribute,necessity,W,W2) =>
    monetaryValue(myFlight,measureFn(N,usDollar),W2)))))).
        
