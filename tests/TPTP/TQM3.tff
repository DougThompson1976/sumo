%3. Bill believes that the Earth is flat.  Is the Earth flat?  Does Bill know that the Earth is flat?
% if you try to circumnavigate a flat earth you will fall off the edge and die
% Does Bill believe he will die if he tries to circumnavigate the Earth?

tff(worlds_tp,type,(w : $tType)).
%tff(individuals_tp,type,($i : $tType)).
tff(modals_tp,type,(m : $tType)).

tff(attribute_tp,type,(attribute : ($i * $i * w) > $o)).
tff(instance_tp,type,(instance : ($i * $i) > $o)).

tff(located_tp,type,(located : ($i * $i * w) > $o)).
tff(agent_tp,type,(agent : ($i * $i * w) > $o)).

tff(earth_tp,type,(earth : $i)).
tff(flat_tp,type,(flat : $i)).
tff(bill_tp,type,(bill : $i)).
tff(dead_tp,type,(dead : $i)).
tff(circumnavigation_tp,type,(circumnavigation : $i)).

tff(believes_tp,type,(believes : m)).
tff(knows_tp,type,(knows : m)).

% modality, agent, consequent world, antecedent world
tff(accreln_tp,type,(accreln : (m * $i * w * w) > $o)).

% tff(axiom_t_believes,axiom,(! [X:$i] : (! [W:w] : (accreln(believes,X,W,W))))).
%tff(axiom_t_knows,axiom,(! [X:$i] : (! [W:w] : (accreln(knows,X,W,W))))).

%(believes Bill (attribute Earth Flat))

tff(kb1,axiom,(
  (![W:w, W2:w] : (accreln(believes,bill,W,W2) =>
    ( (attribute(earth,flat,W2))))))).
    
% believes has fewer successor worlds than knows
% a quantifier over fewer things is easier, therefore
% the implication is "backwards" from what one would
% expect
    
tff(kb3,axiom,(
  (![W:w, W2:w, H:$i]: (accreln(believes,H,W,W2) =>
    accreln(knows,H,W,W2))))).
        
% "Is the earth flat." should be no, satisfiable        
% query: (attribute Earth Flat) - Satisfiable, correct

%tff(conj,conjecture,( ![W:w]: (attribute(earth,flat,W)))).

% "Does bill know that the earth is flat?"
% query: (knows Bill (attribute Earth Flat)) -  Satisfiable, correct (he doesn't know that)

tff(conj,conjecture,( ![W:w, W2:w]: (accreln(knows,bill,W,W2) => (attribute(earth,flat,W2))))).

% If you try to circumnavigate a flat earth you will fall off the edge and die.
% Ignore the temporal aspect for now.
% (=>
%  (and
%    (instance ?C Circumnavigation)
%    (located ?C ?E)
%    (attribute ?E Flat)
%    (agent ?C ?A))
%  (attribute ?A Dead)))

tff(kb4,axiom,
  (![C:$i, E:$i, A:$i, W:w]:
    ((instance(C,circumnavigation) & located(C,E,W) & attribute(E,flat,W) & agent(C,A,W)) =>
     attribute(A,dead,W)))).
     
% We want to say if (X => Y) and A believes X then A believes Y - required true HOL

  
