;All definitions taken from : https://edps.europa.eu/data-protection/data-protection/glossary_en, unless indicated otherwise


; DATA PROCESSING : 
;"any operation or set of operations which is performed on personal data or on sets of personal data, whether or not by automated means, 
; such as collection, recording, organisation, structuring, storage, adaptation or alteration, retrieval, consultation, use, 
; disclosure by transmission, dissemination or otherwise making available, alignment or combination, restriction, erasure or destruction."

(documentation DataProcessing EnglishLanguage "any operation or set of operations which is performed on personal data or on sets of personal data, whether or not by automated means, such as collection, recording, organisation, structuring, storage, adaptation or alteration, retrieval, consultation, use, disclosure by transmission, dissemination or otherwise making available, alignment or combination, restriction, erasure or destruction.")
(subclass DataProcessing ContentDevelopment)       
 
(exists (?PROC ?DATA)
  (and
    (instance ?PROC DataProcessing)
    (instance ?DATA PersonalData)
    (patient ?PROC ?DATA)))
    
    
;PROCESSOR

(documentation DataProcessor EnglishLanguage "A natural or legal person, public authority, agency or other body which processes personal data on behalf of the controller.")

(instance DataProcessor SocialRole)

(=>
  (attribute ?DP DataProcessor)
  (exists (?PROC ?CONTROLLER ?CONTROL)
    (and
      (attribute ?CONTROLLER DataController)
      (instance ?CONTROL Managing)
      (agent ?CONTROL ?CONTROLLER)
      (patient ?CONTROL ?PROC)
      (instance ?PROC DataProcessing)
      (agent ?PROC ?DP))))

   
;Processor agreement

(documentation ProcessorAgreement EnglishLanguage "Transfers of personal data from a data controller to a data processor must be secured by a data processor agreement.")

(subclass ProcessorAgreement Contract)


(=>
  (instance ?PA ProcessorAgreement)
  (exists (?PROCESSOR ?CONTROLLER)
    (and
      (attribute ?PROCESSOR DataProcessor)
      (attribute ?CONTROLLER DataController)
      (partyToAgreement ?CONTROLLER ?PA)
      (partyToAgreement ?PROCESSOR ?PA))))
    
;The contract must stipulate that the data processor shall act only on instructions from the data controller. 

(=>
  (and
    (instance ?PA ProcessorAgreement)
    (attribute ?PROCESSOR DataProcessor)
    (attribute ?CONTROLLER DataController)
    (partyToAgreement ?CONTROLLER ?PA)
    (partyToAgreement ?PROCESSOR ?PA))
  (exists (?INSTRUCT ?ACTION)
    (and
      (instance ?INSTRUCT Ordering)
      (result ?INSTRUCT ?ACTION)
      (holdsObligation
        (<=>
          (agent ?ACTION ?PROCESSOR)
          (agent ?INSTRUCT ?CONTROLLER)) ?PROCESSOR))))
        
     
;The data processor must provide sufficient guarantees in respect of the technical security measures and organisational measure governing the processing to be carried out, and must ensure compliance with such measures.
  
;
;DATA CONTROLLER
;"The data controller is the party that, alone or jointly with others, determines the purposes and means of the processing of personal data."

(documentation DataController EnglishLanguage "The data controller is the party that, alone or jointly with others, determines the purposes and means of the processing of personal data.")

(instance DataController SocialRole)

(=>
  (and
    (instance ?DC AutonomousAgent)
    (attribute ?DC DataController))
  (exists (?PD ?PROC ?MEANS ?PURPOSE ?DETERMINE)
    (and
      (instance ?PD PersonalData)
      (instance ?PROC DataProcessing)
      (instance ?DETERMINE Deciding)
      (instance ?MEANS Method)
      (=>
        (and
          (agent ?DETERMINE ?DC)
          (patient ?DETERMINE ?PD))
        (and
          (hasPurpose ?PD ?PURPOSE)
          (attribute ?PROC ?MEANS))))))
            
;DATA MINIMIZATION : means that a data controller should limit the collection of personal information to what is directly relevant and necessary to accomplish a specified purpose  
; In other words, data controllers should collect only the personal data they really need, and should keep it only for as long as they need it
;expressed by the rule below

;definition I am using : "data controllers should only have the data that they need"

;more detailed: if an agent possesses some data, they should only possess it if they have some purpose to possess that data.


(=>
  (and
    (instance ?DC DataController)
    (instance ?DATA PersonalData)
    (possesses ?DATA ?DC))
  (holdsObligation
    (<=>
      (possesses ?DATA ?DC)
      (hasPurposeForAgent ?DATA
        (possesses ?DATA ?DC) ?DC)) ?DC))
          
;DATA PROTECTION AUTHORUTY (DPA)

(documentation DataProtectionAuthority EnglishLanguage "An independent body that monitors the processing of personal data within its jurisdiction (country, region or international organization);
provides advice to the competent bodies with regard to legislative and administrative measures relating to the processing of personal data;
hears complaints lodged by citizens with regard to the protection of their data protection rights.")

(subclass DataProtectionAuthority AutonomousAgent)

;monitors the processing of personal data within its jurisdiction (country, region or international organization)


;if an entity has a DataProtectionAuthority role and there exists data, that is withing its jursidiction
;then they monitor/supervise that data

;terms: Managing, GeopoliticalArea


(=>
  (and
    (instance ?DPA DataProtectionAuthority)
    (instance ?D PersonalData)
    (instance ?JURISDICTION GeopoliticalArea)
    (jurisdiction ?DPA ?JURISDICTION))
  (exists (?PROC ?MANAGE)
    (and
      (instance ?MANAGE Managing)
      (instance ?PROC DataProcessing)
      (agent ?MANAGE ?DPA)
      (patient ?MANAGE ?PROC)
      (patient ?PROC ?D))))
      
;provides advice to the competent bodies with regard to legislative and administrative measures relating to the processing of personal data


(=>
  (and
    (instance ?BODY AutonomousAgent)
    (instance ?PD PersonalData)
    (instance ?DPA DataProtectionAuthority)
    (instance ?PROC DataProcessing)
    (patient ?PROC ?PD)
    (agent ?PROC ?BODY))
  (exists (?ADVISING ?ADVICE)
    (hasPurpose ?DPA
      (and
        (instance ?ADVISING Request)
        (agent ?ADVISING ?DPA)
        (result ?ADVISING ?ADVICE)
        (destination ?ADVICE ?BODY)
        (containsInformation ?ADVICE ?PD)))))

;hears complaints lodged by citizens with regard to the protection of their data protection rights


; the issue I ran into here: How do I define the topic of the complaint being about Data Protection?
; 

(=>
  (and
    (instance ?CITIZEN Human)
    (instance ?JURISDICTION GeopoliticalArea)
    (instance ?DPA DataProtectionAuthority)
    (jurisdiction ?DPA ?JURISDICTION)
    (instance ?Q Questioning)
    (result ?Q ?QUESTION)
    (origin ?Q ?CITIZEN)
    (instance ?PD PersonalData)
    (refers ?PD ?CITIZEN)
    (refers ?QUESTION ?PD))
  (exists (?LISTEN)
    (and
      (instance ?LISTEN Investigating)
      (holdsObligation
        (and
          (agent ?LISTEN ?DPA)
          (patient ?LISTEN ?QUESTION)) ?DPA))))

         
;CONFIDENTIALITY
;expressed as a rule on PersonalData
;'share data with a person if and only if they are qualified to receive that information


(=>
  (and
    (instance ?PERSON Human)
    (instance ?DATA PersonalData)
    (possesses ?DATA ?PERSON))
  (exists (?RECEIVER ?GIVE)
    (and
      (instance ?RECEIVER Human)
      (instance ?GIVE Giving)
      (holdsObligation
        (<=>
          (and
            (agent ?GIVE ?PERSON)
            (destination ?GIVE ?RECEIVER)
            (patient ?GIVE ?DATA))
          (modalAttribute
            (destination ?GIVE ?RECEIVER) Permission)) ?PERSON))))
    
; DATA COLLECTION
; TODO: do I need this term? It is not defined in the glossary:https://edps.europa.eu/data-protection/data-protection/glossary_en




;DATA MINING :analysing data from different perspectives and summarising it into useful new information

; my interpretation: If there is some data and it is the patient of Data Mining, then there exists some new information that was extracted from it (the original data is the source of it)
; I define 'new information' as information that is not any other information

(documentation DataMining EnglishLanguage "Analysing data from different perspectives and summarising it into useful new information")

(subclass DataMining DataProcessing)

(=>
  (and
    (instance ?MINING DataMining)
    (instance ?DATA ContentBearingObject)
    (patient ?MINING ?DATA))
  (exists (?INFO ?OTHERINFO ?OTHEROBJECT)
    (and
      (containsInformation ?DATA ?INFO)
      (result ?MINING ?INFO)
      (containsInformation ?OTHEROBJECT ?OTHERINFO)
      (not
        (equal ?INFO ?OTHERINFO)))))
      


;
; DATA SUBJECT
; The data subject is the person whose personal data are collected, held or processed.

(documentation DataSubject EnglishLanguage "The data subject is the person whose personal data are collected, held or processed.")

(subclass DataSubject SocialRole)

(=>
  (and
    (instance ?PERSON Human)
    (instance ?SUBJECT DataSubject)
    (attribute ?PERSON ?SUBJECT))
  (exists (?DATA ?ENTITY ?PROCESSING)
    (and
      (instance ?DATA PersonalData)
      (instance ?ENTITY AutonomousAgent)
      (and
        (instance ?PROCESSING DataProcessing)
        (agent ?PROCESSING ?ENTITY)
        (patient ?PROCESSING ?DATA)))))

; PERSONAL DATA
;"‘personal data’ means any information relating to an identified or identifiable natural person (‘data subject’); 
; an identifiable natural person is one who can be identified, directly or indirectly, in particular by reference to an identifier such as a name,
; an identification number, location data, an online identifier or to one or more factors specific to the physical, physiological, genetic, mental, economic, cultural or social identity of that natural person;"

(documentation PersonalData EnglishLanguage "‘personal data’ means any information relating to an identified or identifiable natural person (‘data subject’); an identifiable natural person is one who can be identified, directly or indirectly, in particular by reference to an identifier such as a name, an identification number, location data, an online identifier or to one or more factors specific to the physical, physiological, genetic, mental, economic, cultural or social identity of that natural person;")

(subclass PersonalData ContentBearingObject)

;If data is an instance of Personal Data, then there exists a person that that piece of data refers to and if an agent has access to that data then they can identify the Person from it
(=>
  (instance ?PD PersonalData)
  (exists (?PERSON ?SUBJECT)
    (and
      (instance ?SUBJECT Human)
      (refers ?PD ?SUBJECT)
      (instance ?PERSON Human)
      (knows ?PERSON
        (refers ?PD ?SUBJECT)))))

       
;CCTV
;It is a television system comprised of a camera or a set of cameras monitoring a specific protected area, with additional equipment used for viewing and/or storing the CCTV footage
; this is mapped to SurveillanceSystem in SUMO I believe



;VIDEO SURVEILLANCE

;Question :

(documentation VideoSurveillance EnglishLanguage "Video-surveillance is the monitoring of a specific area, event, activity, or person by means of an electronic device or system for visual monitoring. Typically, the monitoring is carried out using a SurveillanceSystem.")

(subclass VideoSurveillance ContentDevelopment)


;Video surveillance is a Process, that is performed using a camera or a surveillance system and performed on a target 
;Stationary Artifact - use as a word for the target?

(=>
  (instance ?SURVEILLING VideoSurveillance)
  (exists (?CAMERA ?CCTV ?TARGET ?PURPOSE)
    (and
      (instance ?DEVICE ElectricDevice)
      (instance ?TARGET Physical)
      (instrument ?SURVEILLING ?DEVICE)
      (patient ?SURVEILLING ?TARGET)))
 
; rule saying that Surveiling has the purpose of looking at areas to make sure their safe, modeled as : processes that are patient to surveilling should be less dangerous  

(=>
  (instance ?SURVEILLING VideoSurveillance)
  (exists (?PROCESS)
    (hasPurpose ?SURVEILLING
      (decreasesLikelihood
        (=>
          (and
            (instance ?PROCESS Process)
            (patient ?SURVEILLING ?PROCESS))
          (attribute ?PROCESS Dangerous))))))
         
              

;TERMS RELEVANT TO GDPR, BUT NOT DIRECTLY FROM THE GLOSSARY                       
                    
;Monitoring

;meaning : looking at something repetitively with the goal of overseeing its performance          


;jurisdiction

;meaning : the right and power to interpret and apply the law


(documentation jurisdiction EnglishLanguage "(@jurisdiction ?AUTONOMOUSAGENT ?AREA) expresses that an ?AUTONOMOUSAGENT has power to interpret and apply the law in an ?AREA")

(domain jurisdiction 1 AutonomousAgent)	
(domain jurisdiction 2 GeopoliticalArea)

(=>
  (and
    (instance ?AGENT AutonomousAgent)
    (jurisdiction ?AGENT ?AREA))
  (exists (?LAW ?INTERPRET)
    (and
      (modalAttribute ?LAW Law)
      (instance ?INTERPRET Interpreting)
      (instance ?DECIDE LegalDecision)
      (hasRight ?AGENT
        (and
          (agent ?DECIDE ?AGENT)
          (patient ?DECIDE ?LAW)
          (agent ?INTERPRET ?AGENT)
          (patient ?INTERPRET ?LAW))))))




