;;
;; Auto.kif
;; Created on 30th August 2022 at CIRRC in Prague, Czechland by Jennie Pease for a project
;; to generat English sentences using SUMO
;; 
;; 1. terms in file store.txt in bAbl-tasks

(documentation Basket Englishlanguage "A &%Basket is a &%PorousContainer made from interwoven strips of &%Plialbe materials 
such as&%Wicker. The manufacturing &%Process for making baskets is &%Wickering.")
(termFormat EnglishLanguage Basket "basket")
(termFormat ChineseLanguage Basket "筐，篮")
(termFormat ChineseTradiditionalLanguage Basket "筐，籃")
(subclass Basket PorousContainer)

(=>
  (instance ?B Basket)
  (exists (?WARP ?WEFT)
    (and
      (part ?WARP ?B)
      (part ?WEFT ?B)
      (traverses ?WARP ?WEFT)
      (not
        (equal ?WARP ?WEFT)))))
     
(=>
  (instance ?B Basket)
  (material Wicker ?B))

(=>
  (instance ?B Basket)
  (exists (?W)
    (and
      (instance ?W Wickering)
      (result ?W ?B))))

;; related concepts PorousContainer, Sieve, Strainer, Colander. Wicker, PlantWick, SyntheticWicker, Wickering, Weaving

(documentation PorousContainer Englishlanguage "A &%PorousContainer is a &%Container with one or more &%Holes in it, which &%Water, &%Solid or
&%Air can pass through.")
(termFormat EnglishLanguage PorousContainer "porous container")
(termFormat ChineseLanguage PorousContainer "有孔容器")
(termFormat ChineseTradiditionalLanguage PorousContainer "有孔容器")
(subclass PorousContainer Container)

(=>
  (instance ?PC PorousContainer)
  (exists (?HOLE)
    (and
      (instance ?HOLE Hole)
      (hole ?HOLE ?PC))))

(documentation Sieve Englishlanguage "A &%Sieve is a &%PorousContainer. It is a &%Device for &%Removing unwanted &%Substance from desireable ones through
the &%Holes of the &%Sieve.")
(termFormat EnglishLanguage Sieve "sieve")
(termFormat ChineseLanguage Sieve "筲箕")
(termFormat ChineseTradiditionalLanguage Sieve "筲箕")
(subclass Sieve PorousContainer)

(=>
  (instance ?S Sieve)
  (hasPurpose ?S
    (exists (?REMOVE ?MIX ?SUB ?HOLE ?A)
      (and
        (instance ?REMOVE Removing)
        (instance ?MIX Mixture)
        (instance ?SUB Substance)
        (part ?SUB ?MIX)
        (patient ?REMOVE ?SUB)
        (part ?HOLE ?S)
        (instrument ?REMOVE ?HOLE)
        (instance ?A Human)
        (agent ?REMOVE ?A)
        (not
          (desires ?A ?SUB))))))

(subclass Strainer PorousContainer)
(documentation Strainer Englishlanguage "A &%Strainer is a &%PorousContainer. It is a &%Device for &%Removing &%Solid &%Substance from the &%Liquid of 
a &%Mixture.")
(termFormat EnglishLanguage Strainer "strainer")
(termFormat ChineseLanguage Strainer "滤器")
(termFormat ChineseTradiditionalLanguage Strainer "濾器")

(=>
  (instance ?S Strainer)
  (hasPurpose ?S
    (exists (?MIX ?SUB ?LIQUID ?REMOVE)
      (and
        (instance ?MIX Mixture)
        (instance ?SUB Substance)
        (instance ?LIQUID Substance)
        (part ?SUB ?MIX)
        (attribute ?SUB Solid)
        (instance ?REMOVE Removing)
        (part ?LIQUID ?MIX)
        (attribute ?LIQUID Liquid)
        (patient ?REMOVE ?SUB)
        (result ?REMOVE ?LIQUID)
        (instrument ?REMOVE ?S)))))

(subclass Colander PorousContainer)
 (documentation Colander Englishlanguage "A &%Colander is a &%PorousContainer. It is a &%Device for &%Removing &%Liquid &%Substance from the &%Solid &%Substance of 
a &%Mixture.")
(termFormat EnglishLanguage Colander "colander")   
(termFormat ChineseLanguage Colander "滤盆")
(termFormat ChineseTradiditionalLanguage Colander "濾盆")

(=>
  (instance ?C Colander)
  (hasPurpose ?C
    (exists (?MIX ?SUB ?SOLID ?REMOVE)
      (and
        (instance ?MIX Mixture)
        (instance ?SUB Substance)
        (part ?SUB ?MIX)
        (attribute ?SUB Liquid)
        (instance ?REMOVE Removing)
        (patient ?REMOVE ?SUB)
        (instance ?SOLID Substance)
        (part ?SOLID ?MIX)
        (attribute ?SOLID Solid)
        (result ?REMOVE ?SOLID)
        (instrument ?REMOVE ?C)))))

(documentation Wicker Englishlanguage "A &%Wicker is a kind of &%Pliable materials used for &%Wickering. Traditionally, wicker is
made of &%Plant based substance such as willow, rattan, reed or bamboo, but &%SyntheticWicker such as resin or plastic is also available nowadays.")
(termFormat EnglishLanguage Wicker "wicker")
(termFormat ChineseLanguage Wicker "条编材料") 
(termFormat ChineseTradiditionalLanguage Wicker "條編材料")
(subclass Wicker Substance)
(partition Wicker PlantWicker SyntheticWicker)

(=>
  (instance ?W Wicker)
  (attribute ?W Pliable))

(disjoint PlantWicker SyntheticWicker)
(subclass PlantWicker Wicker)
(subclass PlantWicker Tissue)
(subclass PlantWicker PlantSubstance)
(subclass SyntheticWicker Wicker)
(subclass SyntheticWicker Plastic)

(documentation Wickering Englishlanguage "&%Wickering is a way of &%Making &%Furniture or &%Artifacts such as &%Baskets. It is the
oldest furniture making method, dating as far back as 5,000 years ago.")
(termFormat EnglishLanguage Wickering "wickering")
(termFormat ChineseLanguage Wickering "条编")
(termFormat ChineseTradiditionalLanguage Wickering "條編")
(subclass Wickering Weaving)

(=>
  (instance ?W Wickering)
  (hasPurpose ?W
    (exists (?WICK ?OBJ ?WARP ?WEFT)
      (and
        (instance ?WICK Wicker)
        (patient ?W ?WICK)
        (part ?WARP ?WICK)
        (part ?WEFT ?WICK)
        (traverse ?WARP ?WEFT)
        (not
          (equal ?WARP ?WEFT))        
        (result ?W ?OBJ)
        (instance ?OBJ Artifact)))))
      
(documentation Weaving Englishlanguage "&%Weaving is &%Process to interlace yarn or thread such as &%String or pilable materials 
such as wicker for &%Making &%Artifacts.")
(termFormat EnglishLanguage Weaving "weaving")
(termFormat ChineseLanguage Weaving "织")
(termFormat ChineseTradiditionalLanguage Weaving "織")
(subclass Weaving Making)
 
(=>
  (instance ?WEAVE Weaving)
  (hasPurpose ?WEAVE
    (exists (?SUB ?WARP ?WEFT ?OBJ)
      (and
        (instance ?SUB Substance)
        (attribute ?SUB Pliable)
        (patient ?WEAVE ?SUB)
        (part ?WARP ?SUB)
        (part ?WEFT ?SUB)
        (not
          (equal ?WARP ?WEFT))
        (orientation ?WARP ?WEFT RightAngle)
        (result ?WEAVE ?OBJ)
        (instance ?OBJ Artifact)))))
              
;;
;; instance of human called John, Mary, Sandra, Daniel
;; without setting them as "god"
;;

(instance John Human)
(attribute John Actor)
(attribute John Male)

(instance Mary Human)
(attribute Mary Actor)
(attribute Mary Female)

(instance Sandra Human)
(attribute Sandra Actor)
(attribute Sandra Female)

(instance Daniel Human)
(attribute Daniel Actor)
(attribute Daniel Male)

;;
;; Garden, ResidentialGarden and PublicGarden
;;

(documentation Garden Englishlanguage "A &%Garden is a &%CultivatedLandArea where &%Plants are grown for the purpose of &%RecreationOrExercise.")
(termFormat EnglishLanguage Garden "garden")
(termFormat ChineseLanguage Garden " 园林")
(termFormat ChineseTradiditionalLanguage Garden "園林 ")
(subclass Garden CultivatedLandArea)
(partition Garden ResidentialGarden PublicGarden)

(=>
  (and
    (instance ?G Garden)
    (plantInArea Plant ?G))
  (hasPurpose ?G
    (exists (?R)
      (and
        (instance ?R RecreationOrExercise)
        (eventLocated ?R ?G)))))
        
(documentation plantInArea Englishlanguage "&%plantInArea is a &%BinaryRelation which associates the &%class of &%Plant to the area in which they grow.")
(termFormat EnglishLanguage plantInArea "garden")
(termFormat ChineseLanguage plantInArea "栽种于")
(termFormat ChineseTradiditionalLanguage plantInArea "栽種於")
(subclass plantInArea BinaryRelation)
(domainSubClass plantInArea 1 Plant)
(domain plantInArea 2 CultivatedLandArea)
(format EnglishLanguage PlantInArea "%1 &%Located in %2.")

(=>
  (plantInArea Plant ?AREA)
  (exists (?P)
    (and
      (instance ?P Plant)
      (located ?P ?AREA))))

(documentation ResidentialGarden Englishlanguage "A &%ResidentialGarden is a &%Garden adjoining a &%ResidentialBuilding.")
(termFormat EnglishLanguage ResidentialGarden "residential garden")
(termFormat ChineseLanguage ResidentialGarden " 私人庭园")
(termFormat ChineseTradiditionalLanguage ResidentialGarden "私人庭園")
(subclass ResidentialGarden Garden)
(disjoint ResidentialGarden PublicGarden)

(=>
  (instance ?G ResidentialGarden)
  (exists (?B)
    (and
      (instance ?B ResidentialBuilding)
      (orientation ?G ?B Adjacent))))
      
(=>
  (instance ?G ResidentialGarden)
  (modalAttribute
    (exists (?F)
      (and
        (instance ?F Fence)
        (orientation ?G ?F Surrounded))) Likely))

(documentation PublicGarden Englishlanguage "A &%PublicGarden is a &%Garden owned by the &%Government.")
(termFormat EnglishLanguage PublicGarden "public garden")
(termFormat ChineseLanguage PublicGarden " 公共庭园")
(termFormat ChineseTradiditionalLanguage PublicGarden " 公共庭園")
(subclass PublicGarden Garden)

(=>
  (instance ?G PublicGarden)
  (exists (?GOV)
    (and
      (instance ?GOV Government)
      (possesses ?GOV ?G))))

